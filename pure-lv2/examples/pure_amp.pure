
// A simple LV2 audio plugin.

/* To compile this to an LV2 plugin bundle: pure2lv2 pure_amp.pure

   You then need to move the resulting bundle directory pure_amp.lv2 to a
   location on your LV2 search path, such as ~/.lv2 for personal or
   /usr/lib/lv2 for system-wide installation. */

using lv2, system;

/* The `manifest` can be implemented either as a global variable or a
   parameterless function. In either case it must yield a list of port
   descriptions from which the plugin manifest is constructed. Each port
   description is a tuple with the following fields:

   - symbol is the port symbol (a string).

   - name is a more descriptive port name which is typically used as a label
     in the LV2 host GUI.

   - type is the type of port (control, audio, CV, midi), and flags the
     bitwise disjunction of some flags which indicate whether the port is an
     input or output port, along with some additional information. The
     lv2.pure module has symbolic constants for valid type and flags values,
     as well as a number of common type,flag combinations such as
     lv2::controlin, lv2:audioout, etc.

   - default, min and max indicate the default, minimum and maximum values of
     a port, if required. These fields only apply to control or CV ports.

   The first field is mandatory; all other fields are optional. If no port
   name is specified, it is assumed to be the same as the port symbol. If no
   port type is specified, a control port is assumed. If no flags are
   specified, an input port is assumed. If the default, min and max fields are
   omitted, no default value and range is set for the port. This makes it easy
   to specify a basic setup of control input ports by just listing their
   names. For other port types, you'll have to specify at least the port type
   (and the flags, unless it's an input port). */

manifest =
  [// a control port
   ("vol", "Volume", lv2::controlin, 0.5, 0.0, 1.0),
   // audio input and output ports
   ("audioin", lv2::audioin), ("audioout", lv2::audioout)];

/* The `plugin` function is the "main" function of a plugin which is invoked
   with the plugin object (a pointer) as the first argument. The resulting
   closure or partial application is stored in the plugin object, so that
   subsequently the same closure is called each time the plugin is run or its
   activation status changes. This makes it possible to maintain some local
   state in the plugin function (such as the first_run value in the example
   below).

   The plugin function is invoked either with a truth value (true, false) or
   an empty tuple (). In the former case, the activation status (lv2::active)
   has changed, so the plugin can respond to these conditions by adjusting its
   internal state when it becomes active or inactive, respectively.

   The latter case (() argument) indicates that the plugin is run on a block
   of samples. The lv2.pure module provides us with some functions to read the
   input and write the output port values, including the sample data of audio
   ports, in order to implement the functionality of the plugin. */

plugin self = plugin with
  // The local plugin closure gets invoked to process a block of samples.
  plugin () = () when
    // We use a local reference variable here (see below) which lets us
    // remember whether we have invoked this code already. So the following
    // code gets invoked exactly once, at the time the plugin is first run, to
    // print some information about the plugin.
    get first_run ||
    printf "path = %s\ninfo = %s\nrate = %g, block size = %d, vol = %g\n"
    (str (lv2::path self), str (lv2::get_info self), lv2::rate self,
     lv2::nsamples self, lv2::get_port self 0);
    put first_run true;
    // Process a block of samples.
    vol = lv2::get_port self 0; // control value from port #0 (control input)
    wav = lv2::get_port self 1; // sample data from port #1 (audio input)
    // Multiply the sample data with the vol control value and output the
    // resulting wave on port #2 (audio output).
    lv2::set_port self 2 (map (*vol) wav);
  end;
end when
  // Local state. Note that all state which should be local to a single plugin
  // instance must be implemented as a local variable of the plugin function.
  // Global state may be shared between different instances of the same plugin
  // by assigning the state to a global Pure variable.
  first_run = ref false;
end;
